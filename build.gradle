plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version "1.5.5"
}

group = 'com.suzakuwcx'
version = '1.0'


repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'protocollib'
        url = 'https://repo.dmulloy2.net/repository/public/'
    }
    flatDir {
       dirs("server")
    }
}

dependencies {
    paperweight.paperDevBundle("1.20.2-R0.1-SNAPSHOT")
    compileOnly("com.comphenix.protocol:ProtocolLib:5.2.0-SNAPSHOT")
    runtimeOnly ':paper-1.20.2-280'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

// copy generated jar into server plugin directory
tasks.register("run") {
    group 'build'
    dependsOn("reobfJar")
    doLast { 
        copy {
            from reobfJar
            into 'server/plugins'
        }
    }
}